plugins {
    alias libs.plugins.android apply false
    alias libs.plugins.kotlin apply false
    alias libs.plugins.conventions
    alias libs.plugins.versions
}

subprojects {
    group = 'xyz.tynn.hoppa'

    repositories {
        google()
        mavenCentral()
    }

    plugins.withId('com.android.library') {
        project.apply plugin: 'org.jetbrains.kotlin.android'
        project.apply plugin: 'maven-publish'

        java.toolchain {
            languageVersion.set JavaLanguageVersion.of(11)
        }

        android.compileOptions {
            sourceCompatibility 11
            targetCompatibility 11
        }
    }

    plugins.withId('org.jetbrains.kotlin.jvm') {
        project.apply plugin: 'maven-publish'
    }

    plugins.withId('xyz.tynn.convention.kotlin') {
        kotlin.explicitApi()
        dependencies.testImplementation libs.mockk
    }

    plugins.withId('maven-publish') {
        publishing.publications.all {
            pom {
                developers {
                    developer {
                        id = 'tynn'
                        name = 'Christian Schmitz'
                    }
                }
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
            }
        }
    }
}
