//  Copyright 2020 Christian Schmitz
//  SPDX-License-Identifier: Apache-2.0

plugins {
    id 'com.android.library' version '4.2.1' apply false
    id 'org.jetbrains.kotlin.android' version '1.5.10' apply false
    id 'org.jetbrains.dokka' version '0.10.1'
    id 'com.vanniktech.android.junit.jacoco' version '0.16.0'
    id 'xyz.tynn.android.maven' version '0.1.0' apply false
    id 'com.github.ben-manes.versions' version '0.39.0'
    id 'xyz.tynn.idea.fix' version '0.1.2'
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }

    plugins.withType(org.jetbrains.dokka.gradle.DokkaPlugin) {
        if (project != rootProject)
            rootProject.dokka.subProjects += project.name

        tasks.withType(org.jetbrains.dokka.gradle.DokkaTask) {
            configuration {
                externalDocumentationLink {
                    url = new URL('https://developer.android.com/reference/kotlin/')
                    packageListUrl = new URL(url, '../androidx/package-list')
                }
            }
        }
    }
}

subprojects {
    group = 'xyz.tynn.hoppa'

    plugins.withId('com.android.library') {
        project.apply plugin: 'xyz.tynn.android.kdoc'
        project.apply plugin: 'xyz.tynn.android.sources'
        project.apply plugin: 'xyz.tynn.android.maven'
    }

    plugins.withType(com.android.build.gradle.BasePlugin) {
        android {
            compileSdkVersion 30

            defaultConfig {
                minSdkVersion 15
                targetSdkVersion 30
                testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
            }

            compileOptions {
                sourceCompatibility '1.8'
                targetCompatibility '1.8'
            }
        }

        dependencies {
            testImplementation 'io.mockk:mockk:1.11.0'
        }
    }

    plugins.withId('org.jetbrains.kotlin.android') {
        android {
            kotlinOptions {
                jvmTarget = '1.8'
            }
        }

        dependencies {
            implementation 'org.jetbrains.kotlin:kotlin-stdlib'
            testImplementation 'org.jetbrains.kotlin:kotlin-test-junit'
        }
    }

    plugins.withType(MavenPublishPlugin) {
        project.apply from: "$rootDir/publish.gradle"
    }
}

junitJacoco {
    jacocoVersion = '0.8.6'
    includeNoLocationClasses = true
}

dependencyUpdates {
    def isNonStable = { String version ->
        ['alpha', 'beta', 'dev', 'rc', 'cr', 'm', 'preview', 'b', 'ea', 'eap'].any {
            version ==~ /(?i).*[.-]${it}[.\d-+]*.*/
        }
    }
    rejectVersionIf {
        isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
    }
    gradleReleaseChannel = 'current'
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/docs"
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
