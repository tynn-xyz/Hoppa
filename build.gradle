plugins {
    alias libs.plugins.android apply false
    alias libs.plugins.kotlin apply false
    alias libs.plugins.versions
}

subprojects {
    group = 'xyz.tynn.hoppa'

    repositories {
        google()
        mavenCentral()
    }

    plugins.withId('com.android.library') {
        project.apply from: "$rootDir/android.gradle"
        project.apply plugin: 'maven-publish'
        publishing.publications {
            release(MavenPublication) {
                afterEvaluate {
                    from components.release
                }
            }
        }
    }

    plugins.withId('org.jetbrains.kotlin.android') {
        project.apply from: "$rootDir/kotlin.gradle"
    }

    plugins.withId('org.jetbrains.kotlin.jvm') {
        project.apply from: "$rootDir/kotlin.gradle"
        project.apply plugin: 'maven-publish'
        java.withSourcesJar()
        publishing.publications {
            kotlinJvm(MavenPublication) {
                from components.java
            }
        }
    }

    plugins.withId('maven-publish') {
        project.apply from: "$rootDir/publish.gradle"
    }

    tasks.withType(GenerateModuleMetadata).configureEach {
        enabled = false
    }
}

dependencyUpdates {
    def isNonStable = { String version ->
        ['alpha', 'beta', 'dev', 'rc', 'cr', 'm', 'preview', 'b', 'ea', 'eap'].any {
            version ==~ /(?i).*[.-]${it}[.\d-+]*.*/
        }
    }
    rejectVersionIf {
        isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
    }
    gradleReleaseChannel = 'current'
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
